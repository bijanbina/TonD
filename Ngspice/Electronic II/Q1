Question 1

.INCLUDE ua741
*1: common mode voltage source
*2: opamp input
*3: output
$ ////////Op Amp//////////
Voss	Vss+	gnd	DC	10
Voee	Vss-	gnd	DC	-10
*OpAmp	in-	in+	ss+	ss-	out
Xopamp1	1	2	Vss+	Vss-	3	uA741
$ /////////Input/////////
Vcm	1	gnd	DC	0
Vd	2	1	DC	35u	AC	35u
$ /////////Output////////
RL	3	gnd	2K

.control
	$ set plot options
	  set hcopypscolor=0
	$ Common Mode Circuit
	DC	Vcm	10m	2	10m
	let	Gain	=	Abs	(V(3)/	V(1))
	let	Vout	=	V(3)
	hardcopy	'Plot/DC Common Mode Gain.ps'	Gain
	hardcopy	'Plot/DC Common Mode Output.ps'	Vout
	
	$ Differential
	DC	Vd	5u	35u	0.1u
	let	Gain	=	Abs	(V(3)/(V(2)-V(1)))
	let	Vout	=	V(3)
	hardcopy	'Plot/DC Differential Gain.ps'	Gain
	hardcopy	'Plot/DC Differential Vout.ps'	Vout
	
	AC	LIN	100	0.01	2
	let	Gain	=	Abs	(V(3)/(V(2)-V(1)))
	hardcopy	'Plot/AC Differential Gain.ps'	Gain
	
	$ CMRR
	alter	Vcm	DC	=	1	$ <--- change common voltage to 1V
	DC	Vd	5u	35u	0.1u
	let	Gain	=	Abs	(V(3)/(V(2)-V(1)))
	let	a_c	=	V(3)/	V(1)	$ <--- commond mode gain
	let	CMRR	=	Gain/	a_c
	hardcopy	'Plot/CMRR.ps'			CMRR
	shell		./2jpg.sh		$ convert post script to jpg
.endc
.end
